apply plugin: 'maven-publish'
apply plugin: 'signing'
//apply plugin: 'io.codearte.nexus-staging'

ext["signing.keyId"] = System.getenv('SENDSAY_ANDROID_SDK_SIGNING_KEY_ID')
ext["signing.password"] = System.getenv('SENDSAY_ANDROID_SDK_SIGNING_PASSWORD')
ext["signing.secretKeyRingFile"] = System.getenv('SENDSAY_ANDROID_SDK_SIGNING_SECRET_KEY_RING_FILE')
ext["ossrhUsername"] = System.getenv('SENDSAY_ANDROID_SDK_NEXUS_USERNAME')
ext["ossrhPassword"] = System.getenv('SENDSAY_ANDROID_SDK_NEXUS_PASSWORD')

task androidSourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.source
}

nexusStaging {
    packageGroup = 'com.sendsay'
    username = ossrhUsername
    password = ossrhPassword
}

project.afterEvaluate {
    publishing {
        publications {
            sdk(MavenPublication) {
                from components.release
                groupId 'com.sendsay.sdk'
                artifactId 'sdk'
                version android.defaultConfig.buildConfigFields['SENDSAY_VERSION_NAME'].value.replace('"', '')

                artifact androidSourcesJar

                pom {
                    name = 'Sendsay Sdk Library'
                    packaging 'aar'
                    description = 'Sendsay Sdk Library'
                    url = 'https://github.com/sendsay/sendsay-android-sdk'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/license/mit'
                        }
                    }
                    developers {
                        developer {
                            id = 'sendsay'
                            name = 'Sendsay'
                            email = 'contact@sendsay.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/sendsay/sendsay-android-sdk/tree/master'
                        connection = 'scm:https://github.com/sendsay/sendsay-android-sdk.git'
                    }
                    issueManagement {
                        system = 'GitHub issues'
                        url = 'https://github.com/sendsay/sendsay-android-sdk/issues'
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'sonatype'
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}